apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.bretahajek.docus"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 8
        versionName "1.0.6"
        buildFeatures.dataBinding
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        // Add new release type for publishing ARM only APK on github release
        release_arm {
            initWith release
            matchingFallbacks = ['release']
            ndk {
                abiFilters 'armeabi-v7a','arm64-v8a'
            }
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    // DataBinding into layout
    dataBinding {
        enabled true
    }
    android.ndkVersion '21.3.6528147'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'com.google.android.material:material:1.4.0-alpha02'

    // Adding CameraX library
    def camerax_version = "1.1.0-alpha09"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:1.0.0-alpha29"

    // Adding Navigation component
    def nav_version = '2.3.5'
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // OpenCV
    implementation project(path: ':opencv')

    // Adding Room for database
    def room_version = "2.3.0-rc01"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Working with files
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'

    // LiveData, ViewModel
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    // Glide for loading images
    implementation 'com.github.bumptech.glide:glide:4.9.0'

    // Zooming on images
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Exporting optimized PDFs
    implementation 'com.itextpdf:itextg:5.5.10'
}
